# Maintainer: Igor Saric <karabaja4 at gmail.com>
# shellcheck disable=SC2181,SC2148,SC2034,SC2164,SC2154

_gitname=minirc-ng
pkgname=${_gitname}-git
pkgver=0.3.9724c7e
pkgrel=1
pkgdesc="Minimalistic init script (ng)"
arch=('any')
url="https://github.com/karabaja4/${_gitname}"
license=('GPL2')
depends=('busybox' 'bash' 'jq')
makedepends=('git')
optdepends=('systemd: udev implementation')
conflicts=('systemd-sysvcompat' 'sysvinit')
backup=('etc/minirc.json')
source=("git://github.com/karabaja4/${_gitname}.git")
md5sums=('SKIP')

pkgver() {
  cd "${_gitname}"
  echo "0.3.$(git rev-parse --short HEAD)"
}

package() {
  cd ${_gitname}

  install -Dm755 src/rc "${pkgdir}/usr/bin/rc"
  install -Dm755 src/minirc.local "${pkgdir}/etc/minirc.local"
  install -Dm755 src/minirc.local.shutdown "${pkgdir}/etc/minirc.local.shutdown"
  install -Dm644 src/minirc.json "${pkgdir}/etc/minirc.json"
  install -Dm644 src/inittab "${pkgdir}/etc/inittab"

  for i in init halt poweroff reboot
  do
      ln -sf /usr/bin/busybox "${pkgdir}/usr/bin/${i}"
  done
}
